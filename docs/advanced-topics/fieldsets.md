# Fieldsets

### Fieldsets

#### Fieldset creation rules

Here's how the fieldset is calculated:

- include every field defined inside `extra inputs` in the root namespace
- for every `create` and `update` action:
  - namespace equals to `alias`
  - `id` is never included in the fieldset
  - if there's a `set` atom, field is not included in the fieldset
  - if action target is a relationship to an existing record, the parent `reference` is not included in the fieldset
  - if the reference relationship is created via `reference through`, the target field replaces the reference field
  - if there's a `default` in a field specification, the field is **optional**
  - in `update`, inputs must be explicitly declared using `input` atom
  - in `create`, every field not covered by previous rules is included as a required input

Fieldset creation example

```javascript

// let's define our models first
model Org {
  field name { type string }
  field paymentPlan { type string }
  relation memberships { from OrgMembership, through org }
}

model User {
  field email { type string }
  field acceptsTos { type boolean, default false }
  relation orgMemberships { from OrgMembership, through user }
}

model OrgMembership {
  reference org { to Org }
  reference user { to User }
}

api {
  entrypoint Org {
    create endpoint {
      action {
        create as org {
          set paymentPlan "freemium"
        }
        create User as user {}
        // creating org.memberships automatically
        // sets membership.org to `org`
        create org.memberships as membership {
          set user user
        }
      }
    }
  }
}
```

Example of a valid JSON body

```json
{
  "org": { "name": "Acme Inc" },
  "user": { "email": "john.doe@gaudi.tech" }
}
```

Fieldset field creation table

| Alias      | Table name    | Field          | In fieldset                        |
| ---------- | ------------- | -------------- | ---------------------------------- |
| org        | Org           | id             | **no** - id field is autogenerated |
|            |               | **name**       | **required**                       |
|            |               | paymentPlan    | **no** - set on the server side    |
| user       | User          | id             | **no** - id field is autogenerated |
|            |               | **email**      | **required**                       |
|            |               | **acceptsTos** | **optional** - has a default value |
| membership | OrgMembership | id             | **no** - id field is autogenerated |
|            |               | org_id         | **no** - set on the server side    |
|            |               | user_id        | **no** - set on the server side    |

## Validation

### Extra input validation

You can also define a custom `validate` action:

```javascript
extra inputs {
  field passwordRepeat { type string }
}

action {
  create as org {}
  validate with key "passwordRepeat" {
    assert { isEqual(passwordRepeat, org.password) }
  }
}
```
