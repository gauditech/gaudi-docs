---
sidebar_position: 1
sidebar_label: Defining APIs
title: Defining APIs
---

# Defining APIs

APIs are used to expose your data and interact with the outside world (eg. your frontend, clients, ...). Gaudi lets you create REST APIs base on your data model. They are designed to offer a predictable behavior with very little code, which can be customized when needed, or extended with `hooks`.

Using your API definition Gaudi can automatically generate OpenAPI specification and client integration libraries that help you try out, test and integrate your API from day one.

## Defining endpoints

The concept of an API revolves around, `api`, `entrypoint` and `endpoint` blocks. An entrypoint is a group of endpoints which operate on the same data model. Several `entrypoint`s can be grouped into an `api` block.

Endpoints represent specific REST endpoints. Gaudi supports five "built-in" endpoints, and a custom one:

- `create` - uses HTTP `POST`, for creating a new record
- `list` - uses HTTP `GET`, for returning a list of records
- `get` - uses HTTP `GET`, for returning a single record
- `update` - uses HTTP `PATCH`, for updating a single record
- `delete` - uses HTTP `DELETE`, for deleting a single record
- `custom` - completely customizable endpoint

Here is a short example of an api specification:

```javascript
api {
  entrypoint Topic {
    create endpoint {} // POST /api/topic/
    get endpoint {}    // GET /api/topic/{id}/
    custom endpoint {  // POST /api/topic/search/
      POST many "/search"
      ...
    }
    custom endpoint {  // POST /api/topic/{id}/disable/
      POST one "/disable"
      ...
    }
  }
}
```

## Identifying specific records

Some endpoints operate on a whole collection of records, such as:

- `create` endpoint
- `list` endpoint
- `custom` endpoint with `many` cardinality

Others operate on a single record, such as:

- `get` endpoint
- `update` endpoint
- `delete` endpoint
- `custom` endpoint with `one` cardinality

This reflects in an URL for each type of endpoint.

#### Collection endpoints

Collection endpoints require only data model name

```
/api/topic
```

#### Single endpoints

Single endpoints require both data model name and record identifier

```
/api/topic/{identifier}
```

In order to look up a specific record, by default Gaudi expects a record identifier in the URL path. By default, Gaudi uses autogenerated `id` field, but this can be customized using `identify` property.

```
entrypoint Topic {
  identify { through name }
}
```

## Context and aliases

Each `entrypoint` can specify the alias using `as` attribute. An alias from the context is visible in single-cardinality endpoints, as well as in nested entrypoints.

```javascript
entrypoint Topic as topic {
  entrypoint posts as post {
    authorize {
      // this block can see `topic` alias
      // this block can't see `post` alias because it's scoped to a collection of posts
    }
    update endpoint {
      authorize {
        // this block can see both `topic` and `post` aliases
      }
    }
  }
}
```

Aliases from the context can be referenced within `authorize` or `action` blocks.

:::info Read more
Read more about context and aliases - [Actions](./actions#context-and-aliases)
:::

## Authentication

Gaudi supports authentication plugins which let you define authorization methods.

```javascript
auth {
  method basic {}
}
```

There is a special context alias - `@auth`, that contains a record of the currently logged-in user.

## Authorization

Authorization rules can be defined using `authorize` block either on `entrypoint` or on `endpoint` level. It can contain any Gaudi expression which resolves to a boolean value. To access currently authenticated user use `@auth` context alias.

```javascript
api {
  entrypoint Topic {
    // only logged-in users can access topics
    authorize { @auth is not null }
    // only admins can create new topics
    create endpoint {
      authorize { @auth.profile.isAdmin is true }
    }
  }
}
```

## Response schema

Each `entrypoint` is based on a single data model (_target_) and each `endpoint` returns some part of this target model. You can use `response` block to define which part of the target model is returned. `response` can be specified per `entrypoint` or per `endpoint`.

```javascript
api {
  entrypoint Topic {
    response { id, title, author { id, username, profile { imageUrl } } }
  }
}
```

By default, all the fields on the target model will be included. This is also true for relationship fields without its own block.

```
// includes ALL fields of "author" relation
response { id, title, author }

// includes only "name" of "author" relation
response { id, title, author { name } }
```

## Customizing the endpoint action

If you want to extend the default endpoint behavior (required for `custom` endpoints), you can specify the `action` block.

```javascript
entrypoint Organization {
  create endpoint {
    api {
      // default `create` action for this endpoint
      create as org {}
      // assign current user as admin member of the org
      create org.memberships as member {
        set member @auth
        set role "admin"
      }
    }
  }
}
```

:::info
Read more about actions - [Actions](./actions)
:::

## Request body schema

Endpoints that contain `create` or `update` actions may need to accept certain values from the client, via request body; as well as endpoints which contain `extra inputs` directives.

Gaudi analyses the endpoint specification and computes the desired schema. This can be seen in OpenAPI specification, as well as in client integration libraries.

### How is schema calculated

Inputs from `create` and `update` actions are namespaced with the action alias.

```javascript
update org as newOrg {} // inputs are namespaced under "newOrg"
```

Inputs derived from `extra inputs` don't have a namespace.

#### Create actions

By default, a `field` belonging to a model of a target action will be included if:

- it's not an `id` field
- doesn't have `default` value
- is not `set` within action block

You can force a field with a `default` value by explicitly using the `input` directive:

```javascript
create as org {
  input { status { required } }
}
```

If the `required` keyword is ommited, this input will be optional, since it a default value is known.

You can also explicitly set a default value for any input:

```javascript
create as org {
  input { status { default true } }
}
```

#### Update actions

For security reasons, every updateable field must be explicitly specified:

```javascript
update as org {
  input { status, name, logoUrl }
}
```

With `update`, all inputs are optional, unless `required` is set. `default` is not supported in updade actions.

#### Extra inputs

Every `field` specified inside `extra inputs` goes into a root of the schema. It's required, unless `default` is provided.

```javascript
create endpoint {
  extra inputs {
    field passwordRepeat { type string }
    field subscribedToNewsletter { type boolean, default false }
  }
}
```

## Validation

Every `input` derived from `field` in `create`, `update` or `extra inputs` inherits the `validate` blocks specified within a field.

```javascript
model Topic {
  field name { type string, validate { minLength(4) and maxLength(64) }}
}
```

Gaudi will ensure every `input` passes it's `field`'s validation rules.

You can also define a custom `validate` action:

```javascript
extra inputs {
  field passwordRepeat { type string }
}

action {
  create as org {}
  validate with key "passwordRepeat" {
    assert { isEqual(passwordRepeat, org.password) }
  }
}
```
